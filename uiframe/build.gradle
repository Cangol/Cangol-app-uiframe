apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.kt3k.coveralls'
apply plugin: 'net.saliman.cobertura'
apply from: '../maven_push.gradle'
apply from: '../config/quality.gradle'
apply plugin: 'com.jakewharton.hugo'

dependencies {
    compile fileTree(include: '*.jar', dir: 'libs')
    compile 'mobi.cangol.mobile:actionbar:[1.0.27,)@aar'
    compile 'mobi.cangol.mobile:appcore:[1.1.23,)@aar'
    compile 'com.android.support:support-fragment:26.0.0'
    compile 'com.android.support:appcompat-v7:26.0.0'
    compile 'com.isanwenyu.highlight:highlight:1.8.0'
}

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.2'

    defaultConfig {
        minSdkVersion 14
        multiDexEnabled true
        versionCode Integer.decode("${POM_VERSION_CODE}")
        versionName "${POM_VERSION}"
    }
    lintOptions {
        abortOnError false
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    options {
        encoding "UTF-8"
        links "http://docs.oracle.com/javase/7/docs/api/"
        linksOffline "http://d.android.com/reference", "${android.sdkDirectory}/docs/reference"
    }
    destinationDir = file("../docs/")
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}
cobertura.coverageFormats = ['html', 'xml']

tasks.coveralls {
    dependsOn 'check'
}